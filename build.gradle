buildscript {
    configurations.classpath {
        resolutionStrategy {
            //this jgit version is required to fix this issue:
            //https://github.com/diffplug/spotless/issues/587 with the spotless plugin
            force 'org.eclipse.jgit:org.eclipse.jgit:5.7.0.202003110725-r'
        }
    }
}

plugins {
    id "java-library"
    id "jacoco"
    id "com.diffplug.spotless" version "5.7.0"
    id "com.github.ben-manes.versions" version "0.34.0"
    id "pl.allegro.tech.build.axion-release" version "1.12.1"
    id "org.sonarqube" version "3.0"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.5"
    id "com.jfrog.artifactory" version "4.18.0"
    id "org.unbroken-dome.test-sets" version "3.0.1"
    id "nebula.lint" version "16.15.6"
}

group "com.github.michaelruocco"

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "json-masker"
    }
}
project.version = scmVersion.version

repositories {
    mavenCentral()
}

ext {
    slf4jVersion = "1.7.30"
    lombokVersion = "1.18.16"
    jacksonVersion = "2.11.3"

    junitVersion = "5.7.0"
    junitPioneerVersion = "1.0.0"
    mockitoVersion = "3.5.13"
}

testSets {
    integrationTest
}

gradleLint {
    rules = ['all-dependency']
    alwaysRun = false
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.jayway.jsonpath:json-path:2.4.0"
    implementation "org.apache.commons:commons-lang3:3.11"

    testRuntimeOnly "org.slf4j:slf4j-simple:${slf4jVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.assertj:assertj-core:3.18.0"
    testImplementation "org.mockito:mockito-core:3.6.0"

    integrationTestImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"
    integrationTestImplementation "net.javacrumbs.json-unit:json-unit-assertj:2.19.0"
    integrationTestImplementation "com.github.michaelruocco:file-loader:6.0.1"
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
    testLogging {
        events "failed"
        exceptionFormat "full"
        showStandardStreams true
    }
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    dependsOn test, integrationTest
    sourceSets sourceSets.main
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

project.tasks["sonarqube"].dependsOn jacocoTestReport
sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "michaelruocco"
        property "sonar.projectKey", "michaelruocco_json-masker"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/report.xml"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

def pomConfig = {
    licenses {
        license {
            name "BSD License"
            url "http://www.opensource.org/licenses/bsd-license.php"
            distribution "repo"
        }
    }

    developers {
        developer {
            id "mruoc"
            name "Michael Ruocco"
            email "michael.ruocco@hotmail.com"
        }
    }

    scm {
        url "https://github.com/michaelruocco/json-masker"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            pom.withXml {
                def root = asNode()
                root.appendNode("description", "Library to mask fields from a json payload based on json path configurations")
                root.appendNode("name", "json-masker")
                root.appendNode("url", "https://github.com/michaelruocco/json-masker")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["mavenPublication"]
    publish = true

    pkg {
        repo = "maven"
        name = "json-masker"
        userOrg = "michaelruocco"
        licenses = ["bsd"]
        vcsUrl = "https://github.com/michaelruocco/json-masker.git"
        version {
            name = project.version
            desc = project.version
            released = new Date()
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
            }
        }
    }
}

artifactory {
    contextUrl = "http://oss.jfrog.org"
    publish {
        repository {
            repoKey = "oss-snapshot-local"
            username = System.getenv("BINTRAY_USER")
            password = System.getenv("BINTRAY_KEY")
        }
        defaults {
            publications("mavenPublication")
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repoKey = "jcenter"
    }
    clientConfig.info.setBuildNumber(System.getProperty("build.number"))
}

spotless {
    java {
        removeUnusedImports()
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()

        replace 'Not enough space after if', 'if(', 'if ('
        replaceRegex 'Too much space after if', 'if +\\(', 'if ('

        replace 'Not enough space after for', 'for(', 'for ('
        replaceRegex 'Too much space after for', 'for +\\(', 'for ('

        replace 'Not enough space after while', 'while(', 'while ('
        replaceRegex 'Too much space after while', 'while +\\(', 'while ('
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}